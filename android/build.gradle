apply plugin: "maven-publish"
apply plugin: 'signing'

def POM_ARTIFACT_ID = "android"
def AAR_FILE = "libwebrtc.aar"

def configurePom(pom) {
    pom.name = POM_NAME
    pom.packaging = POM_PACKAGING
    pom.description = POM_DESCRIPTION
    pom.url = POM_URL

    pom.scm {
        url = POM_SCM_URL
        connection = POM_SCM_CONNECTION
        developerConnection = POM_SCM_DEV_CONNECTION
    }

    pom.licenses {
        license {
            name = POM_LICENCE_NAME
            url = POM_LICENCE_URL
            distribution = POM_LICENCE_DIST
        }
    }

    pom.developers {
        developer {
            id = POM_DEVELOPER_ID
            name = POM_DEVELOPER_NAME
        }
    }
}

afterEvaluate {

    publishing {
        publications {
            android(MavenPublication) {
                groupId = GROUP
                artifactId = POM_ARTIFACT_ID
                version = VERSION_NAME
                artifact(AAR_FILE)
                artifact("deploy/sources.jar") {
                    classifier 'sources'
                }
                artifact("deploy/javadoc.jar") {
                    classifier 'javadoc'
                }
                configurePom(pom)
            }
        }

        signing {
            publishing.publications.all { publication ->
                sign publication
            }
        }
    }
}